plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id("com.google.devtools.ksp") version "1.5.31-1.0.0"
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

ksp {
    arg("room.schemaLocation","$projectDir/schemas".toString())
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.ammar.platsnprices"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        useIR = true
        freeCompilerArgs += "-Xjvm-default=all" // For Coil 2.x
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

apply from: '../props.gradle'

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.navigation:navigation-compose:2.4.0-alpha10'
    implementation 'androidx.palette:palette-ktx:1.0.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0-rc01"

    // Material
    implementation 'com.google.android.material:material:1.4.0'

    // Compose
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    ksp 'dev.zacsweers.moshix:moshi-ksp:0.14.0'

    // OkHttp
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Coil
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-placeholder-material:$accompanist_version"

    // Exoplayer
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation('org.jetbrains.kotlin:kotlin-reflect') {
        constraints {
            implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version") {
                because 'previous versions have compatibility issue'
            }
        }
    }
}